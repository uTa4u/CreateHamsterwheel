plugins {
    id 'eclipse'
    id 'idea'
    id 'net.neoforged.moddev.legacyforge' version '2.0.126'
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    maven { url = "https://maven.createmod.net" } // Create, Ponder, Flywheel
    maven { url = "https://maven.ithundxr.dev/mirror" } // Registrate
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // ForgeConfigAPIPort
    maven { url = "https://cursemaven.com" }
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

legacyForge {
    version = "$minecraft_version-$forge_version"

    parchment {
        minecraftVersion = minecraft_version
        mappingsVersion = mapping_version
    }

    runs {
        client {
            client()
            gameDirectory = project.file("run")

            systemProperty "mixin.env.remapRefMap", "true"
            systemProperty "mixin.env.refMapRemappingFile", "${projectDir}/build/createSrgToMcp/output.srg"
        }

        server {
            server()
            gameDirectory = project.file("run/server")
        }

        gameTestServer {
            type = "gameTestServer"
            gameDirectory = project.file("run/gametest")
        }

        data {
            data()
            gameDirectory = project.file("run")
            programArguments.addAll '--mod', mod_id as String, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            systemProperty 'forge.logging.console.level', 'debug'
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    modImplementation("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") {
        transitive = false
    }
    modImplementation "net.createmod.ponder:Ponder-Forge-${minecraft_version}:${ponder_version}"
    modCompileOnly "dev.engine-room.flywheel:flywheel-forge-api-${minecraft_version}:${flywheel_version}"
    modRuntimeOnly "dev.engine-room.flywheel:flywheel-forge-${minecraft_version}:${flywheel_version}"
    modImplementation "com.tterrag.registrate:Registrate:${registrate_version}"
    compileOnly annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1")
    implementation "io.github.llamalad7:mixinextras-forge:0.4.1"
    modImplementation "curse.maven:touhou-little-maid-355044:7307720"
    modImplementation "curse.maven:cloth-config-348521:5729105"
    modImplementation "curse.maven:jade-324717:6855440"
}

processResources {
    var replaceProperties = [
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
            mod_license            : mod_license,
            minecraft_version_range: minecraft_version_range,
            loader_version_range   : loader_version_range,
            forge_version_range    : forge_version_range,
    ]

    filesMatching(["META-INF/mods.toml"]) {
        expand(replaceProperties)
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
